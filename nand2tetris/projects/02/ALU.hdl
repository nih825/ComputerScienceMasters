// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y–x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs.
 * Which function to compute is determined by 6 input bits 
 * denoted zx, nx, zy, ny, f, no.
 * The computed function's value is called "out".
 * In addition to computing out, the ALU computes two 
 * 1-bit outputs called zr and ng:
 * if out == 0, zr = 1; otherwise zr = 0;
 * If out < 0, ng = 1; otherwise ng = 0.
 * The 6-bit combinations (zx,nx,zy,ny,f,no) and 
 * their effect are documented in the book. 
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx  == 1) sets x = 0        // 16-bit constant
// if (nx  == 1) sets x = ~x       // bitwise "not"
// if (zy  == 1) sets y = 0        // 16-bit constant
// if (ny  == 1) sets y = ~y       // bitwise "not"
// if (f   == 1) sets out = x + y  // integer 2's-complement addition
// if (f   == 0) sets out = x & y  // bitwise And
// if (no  == 1) sets out = ~out   // bitwise Not
// if (out == 0) sets zr = 1
// if (out < 0)  sets ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if f == 1) or out = x & y (if == 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // Put your code here.
    Not16(in=x,out=notx);
    Inc16(in=notx,out=negatedx);
    Add16(a=x,b=negatedx,out=zero);

    Mux16(a=x,b=zero,sel=zx,out=step1);
    Not16(in=step1,out=notstep1);
    Mux16(a=step1,b=notstep1,sel=nx,out=step2);

    Mux16(a=y,b=zero,sel=zy,out=step3);
    Not16(in=step3,out=notstep3);
    Mux16(a=step3,b=notstep3,sel=ny,out=step4);

    Add16(a=step2,b=step4,out=step5temp);
    And16(a=step2,b=step4,out=step5temp1);
    Mux16(a=step5temp1,b=step5temp,sel=f,out=outtemp);
    Not16(in=outtemp,out=notouttemp);
    Mux16(a=outtemp,b=notouttemp,sel=no,out=out, out[15]=ng,out[0..7]=first,out[8..15]=second);

    
    Or8Way(in=first,out=firsthalf);
    Or8Way(in=second,out=secondhalf);
    Or(a=firsthalf,b=secondhalf,out=tmp);
    Not(in=tmp,out=zr);
}
